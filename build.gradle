plugins {
    id 'java'
}

wrapper {
    gradleVersion = '6.4'
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    jcenter()
}

sourceCompatibility = "${target}"
targetCompatibility = "${target}"

sourceSets.main {
    java {
        srcDirs = ['src']
    }
    resources {
        srcDirs = ['src']
        include '**/*.properties'
        include '**/*.gif'
        include '**/*.png'
        include '**/*.pro'
    }
}

javadoc {
    title          = 'ProGuard Core'
    source         = sourceSets.main.allJava
    classpath      = configurations.compile
    destinationDir = file('docs/api')
    options.addStringOption('Xdoclint:none', '-quiet')
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    compile "org.jetbrains.kotlin:kotlin-stdlib-common:${kotlinVersion}"
    compile "org.jetbrains.kotlinx:kotlinx-metadata-jvm:${kotlinxMetadataVersion}"
}

java {
    withSourcesJar()
    withJavadocJar()
}

ext {
    configureDefaultPublication = {
        groupId = 'net.sf.proguard'
        artifactId = 'proguard-core'
        version = '7.0.0'
        from components.java
        pom {
            name = "[$groupId] $artifactId"
            description = 'ProGuard Core is a free library to read, analyze, modify, and write Java class files.'
            url = 'https://www.guardsquare.com/proguard'
            developers {
                developer {
                    id = 'lafortune'
                    name = 'Eric Lafortune'
                    url = 'https://www.guardsquare.com/proguard'
                    organization = 'Guardsquare'
                    organizationUrl = 'https://www.guardsquare.com/'
                    roles = ['Project Administrator', 'Developer']
                }
            }
            licenses {
                license {
                    name = 'Apache License Version 2.0'
                    url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution = 'repo'
                }
            }
            issueManagement {
                system = 'Github Tracker'
                url = 'https://github.com/Guardsquare/proguard-core/issues'
            }
            scm {
                url = 'https://github.com/Guardsquare/proguard-core.git'
                connection = 'scm:git:https://github.com/Guardsquare/proguard-core.git'
            }
            properties = [
                    'project.build.sourceEncoding': 'UTF-8'
            ]
        }
    }
}

task publish {
    dependsOn 'pub-github:publish'
    dependsOn 'pub-sonatype:publish'
}
